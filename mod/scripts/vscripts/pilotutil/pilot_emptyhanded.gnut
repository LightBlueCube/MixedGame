global function Pilot_EmptyHanded_Init

const string EMPTY_HANDED_MOD_NAME = "pilot_emptyhanded"
const string EMPTY_HANDED_BOOST_MOD_NAME = "pilot_emptyhanded_boost"
const float DEFAULT_RUN_SPEED = 260.0 // no need to change this, I think this refers to animations?

void function Pilot_EmptyHanded_Init()
{
    AddSpawnCallback( "player", OnPlayerEntitySpawned )
}

void function OnPlayerEntitySpawned( entity player )
{
    thread CheckEmptyHandedModLifetime( player )
}

void function CheckEmptyHandedModLifetime( entity player )
{
    player.EndSignal( "OnDestroy" )

    while ( true )
    {
        WaitFrame()
        if ( !IsAlive( player ) ) 
            continue
        if ( player.IsTitan() )
            continue
        entity activeWeapon = player.GetActiveWeapon()
        if ( !IsValid( activeWeapon ) )
            continue
        array<string> mods = activeWeapon.GetMods()
        bool isEmptyHandMod = mods.contains( EMPTY_HANDED_MOD_NAME )
        bool isEmptyHandBoostMod = mods.contains( EMPTY_HANDED_BOOST_MOD_NAME )
        if ( !isEmptyHandMod && !isEmptyHandBoostMod )
            continue
        vector velocity = player.GetVelocity()
        float speedVal = sqrt( velocity.x * velocity.x + velocity.y * velocity.y )
        //print( speedVal )
        if ( speedVal > ( DEFAULT_RUN_SPEED + 1 ) && isEmptyHandMod )
        {
            activeWeapon.RemoveMod( EMPTY_HANDED_MOD_NAME )
            activeWeapon.AddMod( EMPTY_HANDED_BOOST_MOD_NAME )
        }
        else if ( speedVal <= DEFAULT_RUN_SPEED && isEmptyHandBoostMod )
        {
            activeWeapon.RemoveMod( EMPTY_HANDED_BOOST_MOD_NAME )
            activeWeapon.AddMod( EMPTY_HANDED_MOD_NAME )
        }

    }
}