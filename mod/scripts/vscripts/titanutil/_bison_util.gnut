untyped // for player.GetPlayerSettingsMods()
global function BisonUtil_Init

const float BISON_DAMAGE_REDUCTION_SEVERITY = 0.15
const float BISON_SPEED_BOOST_SEVERITY = 0.0 // was 0.15, will cause bison's movement animations messed up
const float BISON_VICTIM_SLOW_DURATION = 0.0 // no slow effect for now
const float BISON_DASH_RECHARGE_RATE_SCALE = 3.0 // since we've removed bison's speed boost, should give them more charge
const float BISON_DASH_RECHARGE_RATE_SCALE_TURBO = 1.75 // bison with pas_mobility_dash_capacity receive less rate
const float BISON_DASH_RECHARGE_DELAY_SCALE = 0.0 // bison won't have any delay recharging another dash 

// bison titan
const float BISON_GRAPPLE_DURATION = 1.0

void function BisonUtil_Init()
{
    AddCallback_OnPilotBecomesTitan( OnPlayerBecomeBison )
    AddSpawnCallback( "npc_titan", OnBisonSpawned )
    AddDamageCallbackSourceID( eDamageSourceId.berserker_melee, BerserkerMelee_DamagedTarget )

    // bison grapple settings
	RegisterSignal( "BisonGrappled" )
    RegisterSignal( "BisonGrappleCancelled" )
    AddCallback_MpAbilityGrapplePrimaryAttack( BisonOnUseGrapple )
}

void function BerserkerMelee_DamagedTarget( entity victim, var damageInfo )
{
    // since we added fighter_melee, should check this...
    entity weapon = DamageInfo_GetWeapon( damageInfo )
    if ( !IsValid( weapon ) )
        return
    if ( !weapon.HasMod( "bison_punch" ) )
        return

    if( !victim.IsTitan() )
        return
    entity soul = victim.GetTitanSoul()
    if( !IsValid( soul ) )
        return
    StatusEffect_AddTimed( soul, eStatusEffect.move_slow, 0.25, BISON_VICTIM_SLOW_DURATION, 0.25 )
    StatusEffect_AddTimed( soul, eStatusEffect.dodge_speed_slow, 0.25, BISON_VICTIM_SLOW_DURATION * 2, 1.0 )
    if( soul.IsDoomed() )
        DamageInfo_SetDamage( damageInfo, DamageInfo_GetDamage( damageInfo ) * 1.5 )
}

void function OnPlayerBecomeBison( entity player, entity titan )
{
    thread HandlePlayerBisonAbility( player )
}

void function OnBisonSpawned( entity titan )
{
    entity soul = titan.GetTitanSoul()
    if( !IsValid( soul ) )
        return
    entity meleeWeapon = titan.GetMeleeWeapon()
    if( !IsValid( meleeWeapon ) )
        return
    if( !meleeWeapon.HasMod( "bison_punch" ) )
        return

    // bison can still execute npc titans, always use scorch prime's
    soul.soul.titanLoadout.titanExecution = "execution_scorch_prime" 
    thread HandleBisonDamageReduction( soul, meleeWeapon )
    
}

void function HandleBisonDamageReduction( entity soul, entity meleeWeapon )
{
    soul.EndSignal( "OnDestroy" )
    meleeWeapon.EndSignal( "OnDestroy" ) // bison's confirmation: melee weapon

    int damageReduction = StatusEffect_AddEndless( soul, eStatusEffect.damage_reduction, BISON_DAMAGE_REDUCTION_SEVERITY )

    OnThreadEnd(
        function(): ( soul, damageReduction )
        {
            if( IsValid( soul ) )
                StatusEffect_Stop( soul, damageReduction )
        }
    )

    WaitForever()
}

void function HandlePlayerBisonAbility( entity player )
{
    wait 0.1
    if( !IsValid( player ) )
        return
    entity soul = player.GetTitanSoul()
    if( !IsValid( soul ) )
        return
    entity meleeWeapon = player.GetMeleeWeapon()
    if( !IsValid( meleeWeapon ) )
        return
    if( !meleeWeapon.HasMod( "bison_punch" ) )
        return

    soul.EndSignal( "OnDestroy" )
    player.EndSignal( "OnDestroy" )
	player.EndSignal( "OnDeath" )
	player.EndSignal( "DisembarkingTitan" )
	player.EndSignal( "TitanEjectionStarted" )
    
    // for spawner using player.GiveExtraWeaponMod( "amped_tacticals" ), force remove it
    /* // now handle by using non-conflict weapon settings
    entity grappleWeapon
    foreach( entity offhand in player.GetOffhandWeapons() )
    {
        if( offhand.GetWeaponClassName() == "mp_ability_grapple" && offhand.HasMod( "bison_grapple" ) )
            grappleWeapon = offhand
    }
    if( IsValid( grappleWeapon ) )
    {
        array<string> mods = grappleWeapon.GetMods()
        mods.removebyvalue( "amped_tacticals" )
        mods.removebyvalue( "all_grapple" )
        print( mods )
        grappleWeapon.SetMods( mods )
    }
    */

    // speed
    int speedBoost = StatusEffect_AddEndless( player, eStatusEffect.speed_boost, BISON_SPEED_BOOST_SEVERITY )
    
    // dash power regen
    array<string> classMods = player.GetPlayerSettingsMods()
    float powerRegenScale = BISON_DASH_RECHARGE_RATE_SCALE
    if ( classMods.contains( "pas_mobility_dash_capacity" ) || classMods.contains( "turbo_titan" ) ) // has turbo engine or in turbo titan mode
        powerRegenScale = BISON_DASH_RECHARGE_RATE_SCALE_TURBO // nerfed rate
    player.SetPowerRegenRateScale( powerRegenScale )
    player.SetDodgePowerDelayScale( BISON_DASH_RECHARGE_DELAY_SCALE )

    OnThreadEnd(
        function(): ( player, soul, speedBoost )
        {
            if( IsValid( player ) )
            {
                // clean up
                StatusEffect_Stop( player, speedBoost )
                player.SetPowerRegenRateScale( 1.0 )
                player.SetDodgePowerDelayScale( 1.0 )
            }
        }
    )

    WaitForever()
}

void function BisonOnUseGrapple( entity weapon, WeaponPrimaryAttackParams attackParams )
{
    // for bison: not allowed to control players or npcs forever by grapple
	if( weapon.HasMod( "bison_grapple" ) )
	{
        entity player = weapon.GetWeaponOwner()
		thread BisonDelayedAutoDetach( player )
	}
}

void function BisonDelayedAutoDetach( entity player )
{
	player.Signal( "BisonGrappled" )
	player.EndSignal( "BisonGrappled" )
    player.EndSignal( "BisonGrappleCancelled" )

    // check cancelling through crouching
	AddButtonPressedPlayerInputCallback( player, IN_DUCK, PlayerPressedGrappleDetachButton )
	AddButtonPressedPlayerInputCallback( player, IN_DUCKTOGGLE, PlayerPressedGrappleDetachButton )

    OnThreadEnd
    (
        function(): ( player )
        {
            if ( IsValid( player ) )
            {
                // clean up
				RemoveButtonPressedPlayerInputCallback( player, IN_DUCK, PlayerPressedGrappleDetachButton )
				RemoveButtonPressedPlayerInputCallback( player, IN_DUCKTOGGLE, PlayerPressedGrappleDetachButton )
            }
        }
    )

	wait BISON_GRAPPLE_DURATION // limited grapple duration
	if( IsAlive( player ) )
		player.Grapple( < 0,0,0 > ) // grapple again while another one is active will cancel it
}

void function PlayerPressedGrappleDetachButton( entity player )
{
	player.Signal( "BisonGrappleCancelled" )
}