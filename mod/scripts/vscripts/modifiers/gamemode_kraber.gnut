global function Modded_Gamemode_Kraber_Init

const array<string> TACTICALS_LIST = 
[
    "mp_ability_heal", 
    "mp_ability_grapple", 
    "mp_ability_holopilot", 
    "mp_weapon_grenade_sonar"
]

const array<string> KRABER_PLAYLIST = 
[
    "mp_glitch",
    "mp_colony02",
    "mp_wargames",
    "mp_eden",
    "mp_drydock",
    "mp_black_water_canal",
    "mp_thaw",
    "mp_grave",
    "mp_homestead",
    "mp_forwardbase_kodai",
    "mp_angel_city",
]

const bool INFINITE_CLIP = false
const bool ENABLE_CHARGE_RIFLE = true

void function Modded_Gamemode_Kraber_Init()
{
    SetHackedDeathEnabled( true ) // powerful thing, no any screen blur and flash on death

    // enable normal settings, since this modifier didn't change so many things
    Nessie_Normal_Settings_NonTitan_Init()

    // gamemode_kraber specific settings
    SetSyncedMeleeNoLimit( true ) // enable execution
	SetShouldFUCKOFFAWALL( true ) // no awall allowed
    SetOverridesBaseForcedPlayList( true ) // overrides base forced playlist
    SetWeaponDropsEnabled( false ) // don't let players pickup other kraber
    //SetBattleChatterEnableToAll( true ) // play chatter to their enemies
    
    Riff_ForceBoostAvailability( eBoostAvailability.Disabled )
	AddCallback_OnPlayerRespawned( OnPlayerRespawned )
	AddCallback_GameStateEnter( eGameState.Playing, OnGameStart )
    AddCallback_OnPlayerGetsNewPilotLoadout( OnPlayerChangeLoadout )
	AddCallback_GameStateEnter( eGameState.Postmatch, OnGamePostmatch )

    // marvin
    AddCallback_GameStateEnter( eGameState.Prematch, OnGamePrematch )
}

void function OnPlayerRespawned( entity player )
{
    // respawn not giving loadout, which will mess things up
	//GiveKraber( player )
    DisableGrappleContent( player ) // make players unable to be grappled
    thread DisableBoostBar( player )
    //Remote_CallFunction_Replay( player, "ServerCallback_SpottingHighlight" ) // temp, just for fun
}

void function OnPlayerChangeLoadout( entity player , PilotLoadoutDef p)
{
	GiveKraber( player )
    //thread DisableBoostBar( player ) // no need to reset boost everytime player try to change loadout
}

void function OnGamePrematch()
{
    Nessie_CreateMenuMarvin()
}

void function OnGameStart()
{
    foreach( entity player in GetPlayerArray() )
        thread DisableBoostBar( player ) // starting will re-enable player's boost, lets set it to disable again

    if ( INFINITE_CLIP )
        thread RestorePlayerKraberClip()
}

void function OnGamePostmatch()
{
    thread KraberForcedPlaylist()
}

void function GiveKraber( entity player )
{
    player.TakeWeaponNow( "melee_pilot_emptyhanded" )

    array<string> allowedWeapons = [ "mp_weapon_sniper" ]
    if ( ENABLE_CHARGE_RIFLE )
        allowedWeapons.append( "mp_weapon_defender" )
    array<string> defaultMods = ["pas_fast_ads", "extended_ammo"]
    if ( INFINITE_CLIP )
        defaultMods = ["pas_fast_ads", "ricochet"]
    
    bool hasKraber = PlayerHasWeapon( player, "mp_weapon_sniper" )
	foreach ( entity weapon in player.GetMainWeapons() )
    {
        string weaponName = weapon.GetWeaponClassName()

        if( !allowedWeapons.contains( weaponName ) )
        {
            player.TakeWeaponNow( weapon.GetWeaponClassName() )

            if ( !hasKraber )
            {
                player.GiveWeapon( "mp_weapon_sniper", defaultMods )
                hasKraber = true
            }
        }

        if ( ENABLE_CHARGE_RIFLE )
        {
            if( weaponName == "mp_weapon_defender" )
            {
                if ( weapon.HasMod( "extended_ammo" ) )
                {
                    weapon.RemoveMod( "extended_ammo" )
                    weapon.SetWeaponPrimaryAmmoCount( weapon.GetWeaponSettingInt( eWeaponVar.ammo_default_total ) ) // update clip
                }
                
                if ( INFINITE_CLIP )
                    weapon.AddMod( "at_unlimited_ammo" ) // unlimited ammo for quick charge
            }
        }
    }

	foreach( entity weapon in player.GetOffhandWeapons() )
	{
		if( !TACTICALS_LIST.contains( weapon.GetWeaponClassName() ) )
			player.TakeWeaponNow( weapon.GetWeaponClassName() )
	}
    if( !IsValid( player.GetOffhandWeapon( OFFHAND_SPECIAL ) ) )
        player.GiveOffhandWeapon( "mp_ability_grapple", OFFHAND_SPECIAL )
}

void function DisableGrappleContent( entity player )
{
    if( IsAlive( player ) )
        player.kv.contents = (int(player.kv.contents) | CONTENTS_NOGRAPPLE) // make player unable to be grappled
}

void function EnableGrappleContent( entity player )
{
    player.kv.contents = (int(player.kv.contents) & ~CONTENTS_NOGRAPPLE) // remove unable to be grappled content
}

void function DisableBoostBar( entity player )
{
    WaitFrame() // wait for player earn meter being set up
    if( IsValid( player ) )
        PlayerEarnMeter_SetMode( player, eEarnMeterMode.DISABLED )
}

void function RestorePlayerKraberClip()
{
    while( GetGameState() == eGameState.Playing )
    {
        foreach ( entity player in GetPlayerArray() )
        {
            entity weapon = player.GetActiveWeapon()
            if ( IsValid( weapon ) )
            {
                if ( weapon.GetWeaponClassName() == "mp_weapon_sniper" )
                    weapon.SetWeaponPrimaryClipCount( weapon.GetWeaponPrimaryClipCountMax() )
            }
        }

        WaitFrame()
    }
}

void function KraberForcedPlaylist()
{
    wait 5
    
    array<string> curPlayList = KRABER_PLAYLIST
    int mapIndex = curPlayList.find( GetMapName() )
    mapIndex += 1 // next map index
	if( mapIndex == curPlayList.len() ) // reached last map?
		mapIndex = 0
	string nextMap = curPlayList[mapIndex]

    ServerCommand( "map " + nextMap )
}
