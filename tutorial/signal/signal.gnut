
void function MainFunc_Init()
{
    RegisterSignal( "ClientConncted" ) // 注册信号
    RegisterSignal( "ClientDisconncted" ) // 注册信号
    AddCallback_OnClientConnected( OnClientConnected )
    AddCallback_OnClientDisconnected( OnClientDisconnected )
}

void function OnClientConnected( entity player )
{
    // 让关卡实体发送"ClientConnected"信号
    svGlobal.levelEnt.Signal( "ClientConncted" ) // svGlobal.levelEnt是服务器的关卡实体，可以接收全局信号
}

void function OnClientDisconnected( entity player )
{
    thread TryRestartMap()
}

void function TryRestartMap()
{
    // EndSignal()，即当实体接收到这个信号时，结束这个线程(这里即结束倒计时)
    svGlobal.levelEnt.EndSignal( "ClientConncted" ) // 玩家加入会发送这个信号，以结束正在进行的倒计时
    // 发送信号，结束上一次的函数
    svGlobal.levelEnt.Signal( "ClientDisconncted" )
    // 发送信号后，立刻检测EndSignal()，这样下次再跑到这个函数时会被上一行发送的信号结束
    svGlobal.levelEnt.EndSignal( "ClientDisconncted" )

    wait 10
    if ( GetPlayerArray().len() != 0 )
        return

    wait 50
    if ( GetPlayerArray().len() == 0 )
        ServerCommand( "map " + GetMapName() )
}