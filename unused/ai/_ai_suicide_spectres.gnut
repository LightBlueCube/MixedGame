global function SuicideSpectres_Init
global function MakeSuicideSpectre
global function SpectreSuicideOnDamaged
global function GetNPCAttackerEnt

// modified!!!
global function SetSuicideSpectreNotExploding // for making pet ticks
global function IsSuicideSpectre // get from file.suicideSpectres

const FX_SPECTRE_EXPLOSION				= $"P_drone_frag_exp"

//
// Suicide spectre script
//

const SPECTRE_EXPLOSION_DELAY 					= 0.25   // Delay for the first spectre in a chain to start exploding.
const SPECTRE_DAMAGE_MULTIPLIER_BODY 			= 1.5
const SPECTRE_DAMAGE_MULTIPLIER_HEAD 			= 6.0
const SPECTRE_DAMAGE_MULTIPLIER_SMART_PISTOL	= 2.0
const SPECTRE_HEADSHOT_KEEP_WALKING_CHANCE		= 100 	 // 35% chance to keep walking after a headshot to add variety

struct
{
	int chainExplosionIndex
	float lastChainExplosionTime

	table< string, array<string> > spectreAnims
	float nextOverloadTime

	// modified!
	array<entity> suicideSpectres
	array<entity> noExplosionSpectres
	table<entity, entity> suicideSpectreChasingTable // for better behavior
} file

const SFX_TICK_OVERLOAD 				= "corporate_spectre_overload_beep"
const SFX_TICK_EXPLODE 					= "corporate_spectre_death_explode"

const SFX_FRAGDRONE_OVERLOAD 			= "weapon_sentryfragdrone_preexplo"
const SFX_FRAGDRONE_EXPLODE 			= "weapon_sentryfragdrone_explo"
const SFX_FRAGDRONE_SUPERPURSUIT		= "weapon_sentryfragdrone_superpursuit"

const CHAIN_EXPLOSION_MAXINDEX			= 10


void function SuicideSpectres_Init()
{
	RegisterSignal( "SuicideSpectreForceExplode" )
	RegisterSignal( "SuicideSpectreExploding" )
	RegisterSignal( "SuicideGotEnemy" )
	RegisterSignal( "SuicideLostEnemy" )

	PrecacheParticleSystem( FX_SPECTRE_EXPLOSION )

	file.spectreAnims[ "spectreSearch" ] <- []
	file.spectreAnims[ "spectreSearch" ].append( "sp_suicide_spectre_search" )
	file.spectreAnims[ "spectreSearch" ].append( "sp_suicide_spectre_search_B" )
	file.spectreAnims[ "spectreSearch" ].append( "sp_suicide_spectre_search_C" )

	AddDamageCallback( "npc_frag_drone", SpectreSuicideOnDamaged_Callback )
	AddDeathCallback( "npc_frag_drone", FragDroneDeath )
	
	// modified!!
	RegisterSignal( "SuicideSpectreEnemyChanged" )
	// did a confirmation for normal spectres
	AddDamageCallback( "npc_spectre", SpectreSuicideOnDamaged_Callback )
	AddDeathCallback( "npc_spectre", FragDroneDeath )
	AddDamageCallbackSourceID( eDamageSourceId.suicideSpectreAoE, SuicideSpectre_DamagedPlayerOrNPC )
}

/************************************************************************************************\

 ######  ######## ######## ##     ## ########
##    ## ##          ##    ##     ## ##     ##
##       ##          ##    ##     ## ##     ##
 ######  ######      ##    ##     ## ########
      ## ##          ##    ##     ## ##
##    ## ##          ##    ##     ## ##
 ######  ########    ##     #######  ##

\************************************************************************************************/
void function MakeSuicideSpectre( entity spectre )
{
	file.suicideSpectres.append( spectre ) // to confirm it's a suicide spectre

	spectre.SetAimAssistAllowed( true )
	spectre.SetAllowMelee( false )
	DisableLeeching( spectre ) // suicide spectres cannot be hacked

	spectre.SetNPCMoveSpeedScale( 1.0 )

	spectre.EnableNPCMoveFlag( NPCMF_IGNORE_CLUSTER_DANGER_TIME | NPCMF_PREFER_SPRINT )
	spectre.DisableNPCMoveFlag( NPCMF_FOLLOW_SAFE_PATHS | NPCMF_INDOOR_ACTIVITY_OVERRIDE )
	
	if( IsSpectre( spectre ) ) // spectres specific
	{
		// prefer chasing players
		TakeAllWeapons( spectre )
		file.suicideSpectreChasingTable[spectre] <- null // for better behavior
	}
	
	spectre.kv.allowShoot = 0

	// Frag drones do suicide spectre behavior but we don't want them doing the enemy changed sounds so filter them out
	if ( !IsFragDrone( spectre ) && !IsTick( spectre ) )
		spectre.SetEnemyChangeCallback( SuicideSpectreEnemyChanged )

	spectre.SetLookDistOverride( SPECTRE_MAX_SIGHT_DIST )
	//spectre.SetHearingSensitivity( 10 ) //1 is default
	spectre.EnableNPCFlag( NPC_MUTE_TEAMMATE )

	spectre.ai.suicideSpectreExplosionDelay = -1

	thread SpectreWaitToExplode( spectre )
	AddAnimEvent( spectre, "frag_drone_armed", FragDroneArmed )
}

void function SetSuicideSpectreNotExploding( entity spectre )
{
	if( !( file.suicideSpectres.contains( spectre ) ) )
		return
		
	if( !( file.noExplosionSpectres.contains( spectre ) ) )
		file.noExplosionSpectres.append( spectre )
}

bool function IsSuicideSpectre( entity spectre )
{
	return file.suicideSpectres.contains( spectre )
}

void function FragDroneArmed( entity npc )
{
	npc.ai.fragDroneArmed = true
}

void function FragDroneDeath( entity spectre, var damageInfo )
{
	FragDroneDeath_Think( spectre, damageInfo )
}

// for reloadscripts
void function FragDroneDeath_Think( entity spectre, var damageInfo )
{
	// modified!!!
	if( !( file.suicideSpectres.contains( spectre ) ) )
		return
	// modified!!!
	bool shouldDoExplosion = true
	if( file.noExplosionSpectres.contains( spectre ) )
		shouldDoExplosion = false
	
	vector pos = spectre.GetOrigin()
	int tagID = spectre.LookupAttachment( "CHESTFOCUS" )
	vector fxOrg = spectre.GetAttachmentOrigin( tagID )
	string expSFX
	if ( spectre.mySpawnOptions_aiSettings == "npc_frag_drone_throwable" )
		expSFX = SFX_FRAGDRONE_EXPLODE
	else
		expSFX = SFX_TICK_EXPLODE
	int expFX = GetParticleSystemIndex( FX_SPECTRE_EXPLOSION )

	entity attacker = DamageInfo_GetAttacker( damageInfo )
	entity attackerEnt = GetNPCAttackerEnt( spectre, attacker )

	int team = GetExplosionTeamBasedOnGamemode( spectre )

	int damageDef = GetDamageDefForFragDrone( spectre )
	
	// try to make ttf1 suicide spectres
	/* no need to use these
	if( IsSpectre( spectre ) )
	{
		int tagID = spectre.LookupAttachment( "CHESTFOCUS" )
		vector origin = spectre.GetAttachmentOrigin( tagID )
		entity damageAttacker = GetNPCAttackerEnt( spectre, attacker )
		Explosion_DamageDefSimple( damagedef_stalker_powersupply_explosion_large, origin, damageAttacker, spectre, origin )
		spectre.Gib( <0, 0, 100> )
	}
	*/
	
	if( shouldDoExplosion )
	{	
		// adjust for spectres, for friendlyfire to work, hardcoding
		// damages from damagedef_frag_drone_throwable_NPC
		if( IsSpectre( spectre ) )
		{
			RadiusDamage(
				pos,									// center
				attackerEnt,							// attacker
				spectre,								// inflictor
				200,									// damage
				300,									// damageHeavyArmor
				200,									// innerRadius
				300,									// outerRadius
				SF_ENVEXPLOSION_MASK_BRUSHONLY,			// flags
				0,										// distanceFromAttacker
				2000,									// explosionForce
				DF_EXPLOSION | DF_GIB,					// scriptDamageFlags
				eDamageSourceId.suicideSpectreAoE )		// scriptDamageSourceIdentifier
		}
		else
			RadiusDamage_DamageDefSimple( damageDef, pos, attackerEnt, spectre, 0 )
		EmitSoundAtPosition( spectre.GetTeam(), pos, expSFX )
		CreateShake( pos, 10, 105, 1.25, 768 )
		StartParticleEffectInWorld( expFX, fxOrg, Vector( 0, 0, 0 ) )
	}
	else // fake explosion
	{
		StartParticleEffectInWorld( GetParticleSystemIndex( $"impact_air_40mm" ), fxOrg, Vector( 0, 0, 0 ) )
		EmitSoundAtPosition( spectre.GetTeam(), pos, "Explo_40mm_Impact_3P" )
	}
	
	spectre.Gib( <0, 0, 100> ) //Used to do .Destroy() on the frag drones immediately, but this meant you can't display the obiturary correctly. Instead, since it's dead already just hide it
}

entity function GetNPCAttackerEnt( entity npc, entity attacker )
{
	entity owner = npc.GetBossPlayer()
	bool ownerIsPlayer = owner != null && owner.IsPlayer()

	if ( IsMultiplayer() )
		return ownerIsPlayer ? owner : npc

	if ( !IsAlive( attacker ) )
		return npc

	// dont give player credit, since that does some bad things
	if ( ownerIsPlayer )
		return owner

	if ( attacker.IsPlayer() )
		return GetEnt( "worldspawn" )

	return attacker
}


int function GetDamageDefForFragDrone( entity drone )
{
	var damageDef = drone.Dev_GetAISettingByKeyField( "damageDefOverride" )
	if ( damageDef != null )
	{
		expect string( damageDef )
		return eDamageSourceId[ damageDef ]
	}

	entity owner = drone.GetBossPlayer()
	if ( owner != null && owner.IsPlayer() )
		return damagedef_frag_drone_throwable_PLAYER

	return damagedef_frag_drone_throwable_NPC
}

void function SuicideSpectre_DamagedPlayerOrNPC( entity victim, var damageInfo )
{
	// only allow suicide spectres damaging themselves as teammates, to trigger chain reactions
	entity attacker = DamageInfo_GetAttacker( damageInfo )
	if( IsValid( attacker ) )
	{
		if( attacker.GetTeam() == victim.GetTeam() && !IsSuicideSpectre( victim ) )
			DamageInfo_SetDamage( damageInfo, 0 )
	}
		
	/* stupid checks, only prevent damaging friendly players
	bool damageProtected = false
	entity attacker = DamageInfo_GetAttacker( damageInfo )
	if( IsValid( attacker ) )
	{
		if( IsSpectre( attacker ) )
		{
			if( attacker.GetTeam() == victim.GetTeam() )
				damageProtected = true
		}
		else
		{
			entity inflictor = DamageInfo_GetInflictor( damageInfo )
			if( IsValid( inflictor ) )
			{
				if( IsSpectre( inflictor ) )
				{
					if( inflictor.GetTeam() == victim.GetTeam() )
						damageProtected = true
				}
			}
		}
	}
	if( damageProtected )
		DamageInfo_SetDamage( damageInfo, 0 )
	*/
}

void function SuicideSpectreEnemyChanged( entity spectre )
{
	// Spectre "Speaks"
	if ( ( RandomFloat( 1.0 ) ) < 0.02 )
		EmitSoundOnEntity( spectre, "diag_imc_spectre_gs_spotenemypilot_01_1" )
	if( IsSpectre( spectre ) ) // modified check for suicide spectres
		spectre.Signal( "SuicideSpectreEnemyChanged" )
}

/************************************************************************************************\

########  ########   #######  ##     ## #### ##     ## #### ######## ##    ##
##     ## ##     ## ##     ##  ##   ##   ##  ###   ###  ##     ##     ##  ##
##     ## ##     ## ##     ##   ## ##    ##  #### ####  ##     ##      ####
########  ########  ##     ##    ###     ##  ## ### ##  ##     ##       ##
##        ##   ##   ##     ##   ## ##    ##  ##     ##  ##     ##       ##
##        ##    ##  ##     ##  ##   ##   ##  ##     ##  ##     ##       ##
##        ##     ##  #######  ##     ## #### ##     ## ####    ##       ##

\************************************************************************************************/
void function SpectreWaitToExplode( entity spectre )
{
	Assert( spectre.IsNPC() )
	spectre.EndSignal( "OnDeath" )

	waitthread SuicideSpectre_WaittillNearEnemyOrExploding( spectre )

	if ( spectre.ai.suicideSpectreExplodingAttacker == null )
	{
		// not exploding, so overload
		spectre.ai.suicideSpectreExplosionDelay = GetSpectreExplosionTime( spectre )
		waitthread SpectreOverloads( spectre )
	}

	if ( spectre.ai.suicideSpectreExplosionDelay > 0 )
		wait spectre.ai.suicideSpectreExplosionDelay

	entity attacker = spectre.ai.suicideSpectreExplodingAttacker
	if ( !IsValid( attacker ) )
	{
		entity lastAttacker = GetLastAttacker( spectre )
		if ( IsValid( lastAttacker ) )
		{
			attacker = lastAttacker
		}
		else
		{
			attacker = spectre
		}
	}

	vector force = GetDeathForce()

	Assert( !attacker.IsProjectile(), "Suicide Spectre attacker was a projectile! Type: " + attacker.ProjectileGetWeaponClassName() )

	// JFS: sometimes the attacker is a projectile, which can cause a script error.
	// The real solution is to figure out which weapon is passing in the projectile as the attacker and correct that.
	if ( attacker.IsProjectile() )
	{
		attacker = spectre
	}
	
	spectre.Die( attacker, attacker, { force = force, scriptType = DF_DOOMED_HEALTH_LOSS, damageSourceId = eDamageSourceId.suicideSpectreAoE } )
}

void function SetSuicideSpectreExploding( entity spectre, entity attacker, float explodingTime )
{
	Assert( spectre.ai.suicideSpectreExplodingAttacker == null )
	spectre.ai.suicideSpectreExplodingAttacker = attacker
	spectre.ai.suicideSpectreExplosionDelay = explodingTime

	spectre.Signal( "SuicideSpectreExploding" )
}

float function GetSpectreExplosionTime( entity spectre )
{
	if ( Time() - file.lastChainExplosionTime > 1.0 )
		file.chainExplosionIndex = 0

	float waitTime = file.chainExplosionIndex * 0.14 // RandomFloatRange( CHAIN_EXPLOSION_INTERVALMIN, CHAIN_EXPLOSION_INTERVALMAX )
	file.lastChainExplosionTime = Time()
	file.chainExplosionIndex++
	return waitTime
}

void function SuicideSpectre_WaittillNearEnemyOrExploding( entity spectre )
{
	spectre.EndSignal( "OnDeath" )
	spectre.EndSignal( "SuicideSpectreExploding" )
	spectre.EndSignal( "SuicideSpectreForceExplode" )

	bool pursuitSoundPlaying = false
	
	float minScale = 0.8 // base stat
	float maxScale = 1.5 // base stat
	if( IsFragDrone( spectre ) )
	{
		minScale = expect float( spectre.Dev_GetAISettingByKeyField( "minSpeedScale" ) )
		maxScale = expect float( spectre.Dev_GetAISettingByKeyField( "maxSpeedScale" ) )
	}	

	while ( true )
	{
		wait 0.1

		if ( !spectre.ai.fragDroneArmed )
			continue

		if ( spectre.ai.suicideSpectreExplodingAttacker != null )
			return

		//If spectre is not interrruptable, don't bother
		if ( !spectre.IsInterruptable() )
			continue

		//If spectre is parented, don't bother
		if ( IsValid( spectre.GetParent() ) )
			continue

		// speed up when near enemy
		entity enemy = spectre.GetEnemy()
		if ( IsAlive( enemy ) )
		{
			float dist = Distance( enemy.GetOrigin(), spectre.GetOrigin() )
			float maxDist = 850
			if ( spectre.mySpawnOptions_aiSettings == "npc_frag_drone_throwable" )
			{
				if ( dist < maxDist )
				{
					if ( pursuitSoundPlaying == false )
					{
						EmitSoundOnEntity( spectre, SFX_FRAGDRONE_SUPERPURSUIT )
						pursuitSoundPlaying = true
					}
				}
				else
				{
					if ( pursuitSoundPlaying == true )
					{
						StopSoundOnEntity( spectre, SFX_FRAGDRONE_SUPERPURSUIT )
						pursuitSoundPlaying = false
					}
				}
			}
			float speed = GraphCapped( dist, 200, 850, maxScale, minScale )
			spectre.SetNPCMoveSpeedScale( speed )
			
			/* // stupid checks!!! not using right now
			if( IsSpectre( spectre ) )
			{	
				entity target = file.suicideSpectreChasingTable[spectre]
				//print( string( spectre ) + "'s enemy is " + string( enemy ) )
				//print( string( spectre ) + "'s chasing target is " + string( target ) )
				if( target != enemy )
				{
					//print( string( spectre ) + "'s target changed to " + string( enemy ) )
					file.suicideSpectreChasingTable[spectre] = enemy
					int followBehavior = GetDefaultNPCFollowBehavior( spectre )
					spectre.InitFollowBehavior( enemy, followBehavior )
					spectre.DisableBehavior( "Assault" )
					spectre.EnableBehavior( "Follow" )
					thread TrackSpectreEnemy( spectre, enemy )
					
					// no need to track dist, since spectre will never clear enemy automatically
					//int chaseDist = expect int( spectre.Dev_GetAISettingByKeyField( "aiEnemy_usePriorityDist" ) )
					//thread TrackEnemyDistance( spectre, enemy, chaseDist )
				}
			}
			*/
		}

		// offset the overload time
		if ( Time() < file.nextOverloadTime )
			continue

		entity attacker = SuicideSpectre_NearEnemy( spectre )
		if ( attacker != null )
		{
			//SetSuicideSpectreOverloading( spectre, attacker )
			//Assert( 0 ) // never reached
			return
		}
	}
}

void function TrackSpectreEnemy( entity spectre, entity enemy )
{
	enemy.EndSignal( "OnDeath" )
	enemy.EndSignal( "OnDestroy" )
	spectre.EndSignal( "OnDeath" )
	spectre.EndSignal( "SuicideSpectreEnemyChanged" )
	
	OnThreadEnd(
		function():( spectre )
		{
			if( IsValid( spectre ) )
			{
				file.suicideSpectreChasingTable[spectre] = null
				spectre.ClearEnemy()
				//print( string( spectre ) + "'s target is cleared" )
				spectre.DisableBehavior( "Follow" )
				spectre.EnableBehavior( "Assault" )
			}
		}
	)

	WaitForever()
}

// prevent spectres chase down players even after they respawn! works pretty bad!
void function TrackEnemyDistance( entity spectre, entity enemy, int chaseDist )
{
	enemy.EndSignal( "OnDeath" )
	enemy.EndSignal( "OnDestroy" )
	spectre.EndSignal( "OnDeath" )
	spectre.EndSignal( "SuicideSpectreExploding" )
	spectre.EndSignal( "SuicideSpectreForceExplode" )
	
	OnThreadEnd(
		function():( spectre )
		{
			if( IsValid( spectre ) )
			{
				file.suicideSpectreChasingTable[spectre] = null
				spectre.ClearEnemy()
				//print( string( spectre ) + "'s target is cleared" )
				spectre.DisableBehavior( "Follow" )
				spectre.EnableBehavior( "Assault" )
			}
		}
	)
	
	while( true )
	{
		if( Distance( enemy.GetOrigin(), spectre.GetOrigin() ) > chaseDist )
			break
		wait 0.2
	}
}

entity function SuicideSpectre_NearEnemy( entity spectre )
{
	// See if any player is close eneough to trigger self-destruct
	array<entity> enemies
	entity closestEnemy = spectre.GetClosestEnemy()
	if ( closestEnemy )
		enemies.append( closestEnemy )

	entity currentEnemy = spectre.GetEnemy()
	if ( currentEnemy && currentEnemy != closestEnemy )
		enemies.append( currentEnemy )

	vector origin = spectre.GetOrigin()

	float dist = 200 // base stat
	if( IsFragDrone( spectre ) )
		dist = expect float( spectre.Dev_GetAISettingByKeyField( "suicideExplosionDistance" ) )
	else if( IsSpectre( spectre ) ) // fuck respawn, have to do this or spectres will stop ahead of players
		dist = float( expect int( spectre.Dev_GetAISettingByKeyField( "chaseStopDist" ) ) )
	foreach ( enemy in enemies )
	{
		if ( !IsAlive( enemy ) )
			continue
		if ( enemy.IsCloaked( true ) )
			continue
		if ( enemy.GetNoTarget() )
			continue
		if ( enemy.IsPlayer() && enemy.IsPhaseShifted() )
			continue

		vector enemyOrigin = enemy.GetOrigin()

		if ( Distance( origin, enemyOrigin ) > dist )
			continue

		float heightDiff = enemyOrigin.z - origin.z

		// dont explode because you jump over me or I am on the floor above you
		if ( fabs( heightDiff ) > 40 )
		{
			// unless enemy is standing on something slightly above you and there is a clear trace
			float curTime = Time()
			float timeDiff = curTime - spectre.ai.suicideSpectreExplosionTraceTime
			const float TRACE_INTERVAL = 2

			if ( heightDiff > 0 && timeDiff > TRACE_INTERVAL && enemy.IsOnGround() && spectre.CanSee( enemy ) )
			{
				spectre.ai.suicideSpectreExplosionTraceTime = curTime
				float frac = TraceHullSimple( origin, < origin.x, origin.y, enemyOrigin.z >, spectre.GetBoundingMins(), spectre.GetBoundingMaxs(), spectre )
				if ( frac == 1.0 )
					return enemy
			}
			continue
		}

		return enemy
	}

	return null
}

void function SpectreOverloads( entity spectre )
{
	spectre.EndSignal( "SuicideSpectreExploding" )
	file.nextOverloadTime = Time() + 0.05

	#if MP
	var chaseTime = spectre.Dev_GetAISettingByKeyField( "SuicideChaseTime" )
	if ( chaseTime != null )
	{
		float maxScale = expect float( spectre.Dev_GetAISettingByKeyField( "maxSpeedScale" ) )
		spectre.SetNPCMoveSpeedScale( maxScale )

		expect float( chaseTime )
		float endChaseTime = Time() + chaseTime

		for ( ;; )
		{
			if ( Time() >= endChaseTime )
				break

			if ( !IsAlive( spectre.GetEnemy() ) )
				break

			entity nearEnemy = SuicideSpectre_NearEnemy( spectre )
			if ( IsAlive( nearEnemy ) )
			{
				if ( nearEnemy.IsTitan() && spectre.IsInterruptable() )
				{
					JumpAtTitan( spectre, nearEnemy )
					spectre.ai.suicideSpectreExplosionDelay = 0.0
					return
				}
				break
			}

			WaitFrame()
		}
	}
	#endif
	
	// try to make ttf1 suicide spectres
	entity nukeFXInfoTarget
	entity fx
	if( IsSpectre( spectre ) )
	{
		//print( "Try to apply fx to suicide spectre" )
		nukeFXInfoTarget = CreateEntity( "info_target" )
		nukeFXInfoTarget.kv.spawnflags = SF_INFOTARGET_ALWAYS_TRANSMIT_TO_CLIENT
		DispatchSpawn( nukeFXInfoTarget )

		nukeFXInfoTarget.SetParent( spectre, "CHESTFOCUS" )
	
		fx = PlayFXOnEntity( $"P_spectre_suicide_warn", nukeFXInfoTarget )
	}

	for ( ;; )
	{
		#if SP
		if ( spectre.IsInterruptable() && !spectre.Anim_IsActive() )
			break
		#elseif MP
		if ( spectre.IsInterruptable() && !spectre.Anim_IsActive() && spectre.IsOnGround() )
			break
		#endif

		WaitFrame()
	}

	string overloadSF
	bool isFragDrone = 	spectre.mySpawnOptions_aiSettings == "npc_frag_drone_throwable"
	if ( isFragDrone )
		overloadSF = SFX_FRAGDRONE_OVERLOAD
	else
		overloadSF = SFX_TICK_OVERLOAD
	// Overload Sound
	EmitSoundOnEntity( spectre, overloadSF )

	AI_CreateDangerousArea_DamageDef( damagedef_frag_drone_explode, spectre, TEAM_INVALID, true, false )

	// Cleanup on thread end
	OnThreadEnd(
		//function() : ( spectre, overloadSF )
		// try to make ttf1 suicide spectres
		function() : ( spectre, overloadSF, nukeFXInfoTarget, fx )
		{
			if ( IsValid( spectre ) )
			{
				StopSoundOnEntity( spectre, overloadSF )
			}
			
			// try to make ttf1 suicide spectres
			if( IsValid( nukeFXInfoTarget ) )
			{
				nukeFXInfoTarget.Destroy()
			}
			if( IsValid( fx ) )
			{
				EffectStop( fx )
			}
		}
	)

	bool jumpAtTitans = spectre.Dev_GetAISettingByKeyField( "JumpAtTitans" ) == null || spectre.Dev_GetAISettingByKeyField( "JumpAtTitans" ) == 1
	if( IsSpectre( spectre ) )
		jumpAtTitans = true

	entity enemy = spectre.GetEnemy()
	if ( enemy && enemy.IsTitan() && jumpAtTitans && !spectre.IsInterruptable() )
	{
		JumpAtTitan( spectre, enemy )
	}
	else
	{
		string anim = "sp_suicide_spectre_explode_stand"
		var overrideAnim = spectre.Dev_GetAISettingByKeyField( "OverrideOverloadAnim" )

		if ( overrideAnim != null )
		{
			anim = expect string( overrideAnim )
		}
		else if( IsSpectre( spectre ) ) // suicide spectres specific
		{
			anim = "sp_suicide_spectre_explode"
		}

		waitthread PlayAnim( spectre, anim )

		//if ( !isFragDrone ) // god no need to do this
		//	wait 0.25
	}
}

void function JumpAtTitan( entity spectre, entity enemy )
{
	vector myOrigin = spectre.GetOrigin()
	vector dirToEnemy = enemy.EyePosition() - myOrigin

	float dist = Length( dirToEnemy )
	if ( dist > 0 )
	{
		const float MAX_DIST = 100
		dirToEnemy *= min( MAX_DIST, dist ) / dist
	}

	vector refOrigin = myOrigin + Vector( dirToEnemy.x, dirToEnemy.y, 256 )
	vector refAngles = spectre.GetAngles() + Vector( 0, 180, 0 )
	spectre.Anim_ScriptedPlayWithRefPoint( "sd_jump_explode", refOrigin, refAngles, 0.3 )
	WaittillAnimDone( spectre )
	return
}

int function GetExplosionTeamBasedOnGamemode( entity spectre )
{
	return spectre.GetTeam()
}


/************************************************************************************************\

########     ###    ##     ##    ###     ######   ########
##     ##   ## ##   ###   ###   ## ##   ##    ##  ##
##     ##  ##   ##  #### ####  ##   ##  ##        ##
##     ## ##     ## ## ### ## ##     ## ##   #### ######
##     ## ######### ##     ## ######### ##    ##  ##
##     ## ##     ## ##     ## ##     ## ##    ##  ##
########  ##     ## ##     ## ##     ##  ######   ########

\************************************************************************************************/
void function SpectreSuicideOnDamaged_Callback( entity spectre, var damageInfo )
{
	SpectreSuicideOnDamaged( spectre, damageInfo )
}


void function SpectreSuicideOnDamaged( entity spectre, var damageInfo )
{
	if( !( file.suicideSpectres.contains( spectre ) ) )
		return
	//Assert( IsSuicideSpectre( spectre ) )

	int damageType = DamageInfo_GetCustomDamageType( damageInfo )
	DamageInfo_SetCustomDamageType( damageInfo, damageType )

	if ( !IsAlive( spectre ) )
		return


	entity attacker 			= DamageInfo_GetAttacker( damageInfo )
	entity inflictor 			= DamageInfo_GetInflictor( damageInfo )
	float damage				= DamageInfo_GetDamage( damageInfo )
	int damageSourceId 			= DamageInfo_GetDamageSourceIdentifier( damageInfo )

	// Calculate build time credit
	if ( attacker.IsPlayer() )
	{
		if ( GameModeRulesShouldGiveTimerCredit( attacker, spectre, damageInfo ) && !TitanDamageRewardsTitanCoreTime() )
		{
			float timerCredit = CalculateBuildTimeCredit( attacker, spectre, damage, spectre.GetHealth(), spectre.GetMaxHealth(), "spectre_kill_credit", 9 )
			if ( timerCredit )
				DecrementBuildTimer( attacker, timerCredit )
		}
	}

	// No pain anims for suicide spectres
	DamageInfo_AddDamageFlags( damageInfo, DAMAGEFLAG_NOPAIN )


	spectre.Signal( "SuicideSpectreExploding" )

	if ( !IsValid( inflictor ) || !inflictor.IsPlayer() )
	{
		if ( spectre.ai.suicideSpectreExplodingAttacker == null )
		{
			if ( spectre.GetHealth() - damage <= 0 || ( IsValid( inflictor ) && IsTick( inflictor ) ) )
			{
				float explosionTime = GetSpectreExplosionTime( spectre )
				SetSuicideSpectreExploding( spectre, attacker, explosionTime )
				DamageInfo_SetDamage( damageInfo, 0 )
				return
			}
		}
		else
		{
			// already exploding
			DamageInfo_SetDamage( damageInfo, 0 )
			return
		}

		DamageInfo_SetDamage( damageInfo, damage )
	}
}
